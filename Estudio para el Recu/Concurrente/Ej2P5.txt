{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso identificar
  comenzar
    EnviarMensaje (1,r1)
    EnviarMensaje (2,r2)
    EnviarMensaje (3,r3)
  fin
  proceso juntarPapeles
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
  fin
  
  proceso juntarFlores(ES cantF: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cantF:=cantF+1
  fin
  proceso depositarFlores (ES cantF: numero)
  variables
    miCalle: numero
    miAvenida: numero
  comenzar
    si (cantF<>0)
      miCalle:=PosCa
      miAvenida:=PosAv
      si (cantF<>0)
        BloquearEsquina (50,50)
        Pos (50,50)
        repetir cantF
          depositarFlor
        Pos (miAvenida,miCalle)
        LiberarEsquina (50,50)
        cantF:=0
  fin
  proceso sincronizarse (E id: numero)
  variables
    ok: boolean
  comenzar
    si id=1
      EnviarMensaje (V,r2)
      EnviarMensaje (V,r3)
    sino
      si id=2
        EnviarMensaje (V,r1)
        EnviarMensaje (V,r3)
      sino
        EnviarMensaje (V,r1)
        EnviarMensaje (V,r2)
    RecibirMensaje (ok,*)
    RecibirMensaje (ok,*)
  fin
areas
  ciudad: AreaC (1,1,100,100)
robots
  robot robot1
  variables
    id: numero
    cantF: numero
  comenzar
    RecibirMensaje (id,jefecito)
    cantF:=0
    derecha
    repetir 9
      repetir 10
        juntarFlores (cantF)
        juntarPapeles 
        mover
      juntarFlores (cantF)
      juntarPapeles
      depositarFlores (cantF)
      sincronizarse (id)
    repetir 9
      juntarFlores (cantF)
      juntarPapeles 
      mover
    juntarFlores (cantF)
    juntarPapeles 
    depositarFlores (cantF)
    EnviarMensaje (V,jefecito)
    Informar (id)
  fin
  robot jefe
  variables
    cantF: numero
    ok: boolean
  comenzar
    identificar 
    repetir 3
      RecibirMensaje (ok,*)
    Pos (50,50)
    juntarFlores (cantF)
    Informar (cantF)
  fin
variables
  r1: robot1
  r2: robot1
  r3: robot1
  jefecito: jefe
comenzar
  AsignarArea(r1, ciudad)
  AsignarArea(r2, ciudad)
  AsignarArea(r3, ciudad)
  AsignarArea(jefecito, ciudad)
  Iniciar(r1, 1,1)
  Iniciar(r2, 1,2)
  Iniciar(r3, 1,3)
  Iniciar(jefecito, 1,4)
fin